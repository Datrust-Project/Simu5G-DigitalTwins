#!/usr/bin/gnuplot -persist

set terminal postscript enhanced color 

set view map
set size ratio 1
set size 0.7,0.7

set datafile missing "NaN"
set datafile separator ","

set grid back lc rgb "gray" lw 1 lt 1

# --- Key --- #
set key box opaque reverse Left left font ",14"

# --- Styles definition --- #
set style fill solid border lc rgb "black"
set style line 1 lc rgb "#00BB00" lw 1.5
set style line 2 lc rgb "red" lw 1.5
set style line 3 lc rgb "blue" lw 1.5
set style line 4 lc rgb "red" lw 1.5
set style line 5 lc rgb "brown" lw 1.5
set style line 6 lc rgb "orange" lw 1.5

# style for errorbars
set style line 11 lc rgb "black" ps 0 lw 1.5


set boxwidth 0.6
set style fill solid
set xrange [-2:14]
set xlabel "Number of UEs"

set yrange [0:*]
set ylabel "Allocated RBs (DL)" font ",16" offset 1

set output "stats-backgroundUes/backgroundUes-blocksDl.ps"
plot "stats-backgroundUes/avg_blocksDl.csv" using ($1-0.6):7 with boxes ls 2 t "FG UEs",\
                                         "" using ($1-0.6):7:8 with yerrorbars ls 11 notitle,\
                                         "" using 1:3:xtic(2) with boxes ls 1 t "BG UEs, Real. CQI",\
                                         "" using 1:3:4 with yerrorbars ls 11 notitle,\
                                         "" using ($1+0.6):5 with boxes ls 3 t "BG UEs, Prob. CQI",\
                                         "" using ($1+0.6):5:6 with yerrorbars ls 11 notitle


set yrange [0:*]
set ylabel "Allocated RBs (UL)" font ",16" offset 1

set output "stats-backgroundUes/backgroundUes-blocksUl.ps"
plot "stats-backgroundUes/avg_blocksUl.csv" using ($1-0.6):7 with boxes ls 2 t "FG UEs",\
                                         "" using ($1-0.6):7:8 with yerrorbars ls 11 notitle,\
                                         "" using 1:3:xtic(2) with boxes ls 1 t "BG UEs, Real. CQI",\
                                         "" using 1:3:4 with yerrorbars ls 11 notitle,\
                                         "" using ($1+0.6):5 with boxes ls 3 t "BG UEs, Prob. CQI",\
                                         "" using ($1+0.6):5:6 with yerrorbars ls 11 notitle

                                         
set yrange [18.5:20.5]
set ylabel "RTT [ms]" font ",16" offset 1
to_ms(x) = x*1000

set output "stats-backgroundUes/backgroundUes-rtt.ps"
plot "stats-backgroundUes/avg_rtt.csv" using ($1-0.6):(to_ms($7)) with boxes ls 2 t "FG UEs",\
                                         "" using ($1-0.6):(to_ms($7)):(to_ms($8)) with yerrorbars ls 11 notitle,\
                                         "" using 1:(to_ms($3)):xtic(2) with boxes ls 1 t "BG UEs, Real. CQI",\
                                         "" using 1:(to_ms($3)):(to_ms($4)) with yerrorbars ls 11 notitle,\
                                         "" using ($1+0.6):(to_ms($5)) with boxes ls 3 t "BG UEs, Prob. CQI",\
                                         "" using ($1+0.6):(to_ms($5)):(to_ms($6)) with yerrorbars ls 11 notitle